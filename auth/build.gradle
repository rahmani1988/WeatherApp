plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.reza.auth'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "String", "GOOGLE_SING_IN_WEB_CLIENT_ID", getGoogleSignInWebClientID()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        staging {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {

    // Projects
    implementation project(':core')
    testImplementation project(':tests')

    implementation rootProject.playServicesAuth

    // Android Test
    androidTestImplementation rootProject.androidXjunit
    androidTestImplementation rootProject.espresso
}

def getGoogleSignInWebClientID() {
    // to get the variable from remote
    def googleSignInWebClientId = System.getenv("GOOGLE_SING_IN_WEB_CLIENT_ID")
    if (googleSignInWebClientId == null || googleSignInWebClientId.length() == 0) {
        // to get the variable from local
        def localFile = file("../local.properties")
        def properties = new Properties()
        properties.load(new FileInputStream(localFile))
        googleSignInWebClientId = properties['GOOGLE_SING_IN_WEB_CLIENT_ID']
        if (googleSignInWebClientId == null) {
            logger.error("You need to either place appropriate local.properties or set proper environment variables on Jenkins for GOOGLE SING IN WEB CLIENT ID key")
        }
    }
    return googleSignInWebClientId
}